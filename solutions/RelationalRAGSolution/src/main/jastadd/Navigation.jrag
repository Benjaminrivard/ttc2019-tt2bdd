aspect Navigation {

  // TruthTable

  inh TruthTable Port.containingTruthTable();
  inh TruthTable Row.containingTruthTable();
  inh TruthTable PortOrder.containingTruthTable();
  inh TruthTable Cell.containingTruthTable();
  eq TruthTable.getPort().containingTruthTable() = this;
  eq TruthTable.getRow().containingTruthTable() = this;
  eq TruthTable.getNaturalPortOrder().containingTruthTable() = this;
  eq TruthTable.getHeuristicPortOrder().containingTruthTable() = this;

  syn List<OutputPort> TruthTable.outputPorts() {
    List<OutputPort> result = new ArrayList<>();
    for (Port port: getPortList()) {
      if (!port.isInput()) {
        result.add(port.asOutput());
      }
    }
    return Collections.unmodifiableList(result);
  }

  syn List<InputPort> TruthTable.inputPorts() {
    List<InputPort> result = new ArrayList<>();
    for (Port port: getPortList()) {
      if (port.isInput()) {
        result.add(port.asInput());
      }
    }
    return Collections.unmodifiableList(result);
  }

  syn boolean Port.isInput() = false;
  eq InputPort.isInput() = true;

  syn InputPort Port.asInput() = null;
  eq InputPort.asInput() = this;

  syn OutputPort Port.asOutput() = null;
  eq OutputPort.asOutput() = this;

  syn Boolean Row.valueFor(Port port) {
    for (Cell cell : getCellList()) {
      if (cell.getPort().equals(port)) {
        return cell.getValue();
      }
    }
    return null;
  }

  syn Boolean TerminalNode.valueFor(Port port) {
    for (Assignment assignment : getAssignmentList()) {
      if (assignment.getPort().equals(port)) {
        return assignment.getValue();
      }
    }
    return null;
  }

  // BDT

  syn boolean Tree.isSubtree() = false;
  eq Subtree.isSubtree() = true;

  syn Subtree Tree.asSubtree() = null;
  eq Subtree.asSubtree() = this;

  // BDD

  syn boolean AbstractNode.isInnerNode() = false;
  eq InnerNode.isInnerNode() = true;

  syn InnerNode AbstractNode.asInnerNode() = null;
  eq InnerNode.asInnerNode() = this;

  // Shared Attributes

  inh ASTNode ASTNode.root();
  eq TruthTable.getPort().root() = this;
  eq TruthTable.getRow().root() = this;
  eq TruthTable.getNaturalPortOrder().root() = this;
  eq TruthTable.getHeuristicPortOrder().root() = this;

  eq BDT.getTree().root() = this;

  eq BDD.getAbstractNode().root() = this;

  syn ASTNode TruthTable.root() = this;
  syn ASTNode BDT.root() = this;

}
