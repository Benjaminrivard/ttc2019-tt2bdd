aspect Analysis {

  syn Boolean Row.valueFor(Port port) {
    for (Cell cell : getCellList()) {
      if (cell.getPort().equals(port)) {
        return cell.getValue();
      }
    }
    return null;
  }

  syn Boolean TerminalNode.valueFor(Port port) {
    for (Assignment assignment : getAssignmentList()) {
      if (assignment.getPort().equals(port)) {
        return assignment.getValue();
      }
    }
    return null;
  }

  syn int BDD.decisionNodeCount() {
    int result = 0;
    for (AbstractNode node: getAbstractNodeList()) {
      if (node.isInnerNode()) {
        result++;
      }
    }
    return result;
  }

  syn int BDD.assignmentNodeCount() {
    return getNumAbstractNode() - decisionNodeCount();
  }

  syn int BDT.decisionNodeCount() = getTree().decisionNodeCount();

  syn int Tree.decisionNodeCount();
  eq Subtree.decisionNodeCount() = 1 + getTreeForZero().decisionNodeCount() + getTreeForOne().decisionNodeCount();
  eq Leaf.decisionNodeCount() = 0;

  syn int BDT.assignmentNodeCount() = getTree().assignmentNodeCount();

  syn int Tree.assignmentNodeCount();
  eq Subtree.assignmentNodeCount() = getTreeForZero().assignmentNodeCount() + getTreeForOne().assignmentNodeCount();
  eq Leaf.assignmentNodeCount() = 1;
}
